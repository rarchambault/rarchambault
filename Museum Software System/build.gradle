// Required plugins
plugins {
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'java'
	// Code coverage tool
	id 'jacoco'
	// Postman tests
	id 'com.github.michaelruocco.gradle-postman-runner' version '0.1.4'
}

// Package name
group = 'ca.mcgill.esce321'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

// Required dependencies
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Run all tests when building the project
tasks.named('test') {
	// Run tests on JUnit
	useJUnitPlatform()
	
	// Print full exceptions to help debugging
	testLogging {
		exceptionFormat "full"
	}
	// Generate the coverage report whenever the tests are run
	finalizedBy 'jacocoTestReport'
}

tasks.named('jacocoTestReport') {
	// The tests must be run before the coverage report can be generated
	dependsOn 'test'
}

// Enforce minimum instruction coverage goal
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}
	}
}


